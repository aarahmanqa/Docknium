plugins {
    id 'java'
    id 'maven-publish'
    id 'signing' // only needed for Maven Central
}

group = 'io.github.aarahman7'
version = '0.0.1'

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = 'io.github.aarahman7'
            artifactId = 'docknium'
            version = '0.0.1'

            pom {
                name = 'Docknium'
                description = 'Utility library for managing Selenium Grid with Docker and Selenium Java.'
                url = 'https://github.com/aarahman7/Docknium'

                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id = 'aarahman7'
                        name = 'Ahamed Abdul Rahman'
                        email = 'ahamedabdulrahman@gmail.com' // replace with your preferred public email
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/aarahman7/Docknium.git'
                    developerConnection = 'scm:git:ssh://github.com/aarahman7/Docknium.git'
                    url = 'https://github.com/aarahman7/Docknium'
                }
            }
        }
    }

    repositories {
        // For local testing
        maven {
            name = "localTest"
            url = layout.buildDirectory.dir("repo")
        }

        // For Maven Central (Sonatype OSSRH)
        maven {
            name = "ossrh-staging-api"
            def releasesRepoUrl = uri("https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/")
            def snapshotsRepoUrl = uri("https://ossrh-staging-api.central.sonatype.com/content/repositories/snapshots/")
            url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = project.findProperty("ossrhUsername") ?: ""
                password = project.findProperty("ossrhPassword") ?: ""
            }
        }
    }
}

dependencies {
    //TestNG
    compileOnly 'org.testng:testng:7.7.0'
    runtimeOnly 'org.testng:testng:7.7.0'
    annotationProcessor 'org.testng:testng:7.7.0'
    testImplementation 'org.testng:testng:7.7.0'
    implementation 'org.testng:testng:7.7.0'

    //Docker
    implementation 'com.github.docker-java:docker-java:3.4.1'
    implementation 'com.github.docker-java:docker-java-transport-httpclient5:3.4.1'

    //Selenium
    implementation 'org.seleniumhq.selenium:selenium-java:4.31.0'
    implementation 'org.seleniumhq.selenium:selenium-devtools-v134:4.31.0'

    // Logging dependencies
    implementation 'org.slf4j:slf4j-api:2.0.9'
    implementation 'ch.qos.logback:logback-classic:1.5.13'

    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    compileOnly 'org.projectlombok:lombok:1.18.30'
}

test {
    useTestNG()
}

signing {
    sign publishing.publications.mavenJava
}